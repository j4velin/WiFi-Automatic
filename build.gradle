buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        if (rootProject.ext.has("gradle_classpath")) {
            classpath rootProject.ext.gradle_classpath
        } else {
            logger.warn('classpath not found in rootProject')
            classpath 'com.android.tools.build:gradle:1.2.3'
        }
    }
}

apply plugin: 'com.android.application'

android {

    if (rootProject.ext.has("compileSdkVersion")) {
        compileSdkVersion rootProject.ext.compileSdkVersion
        buildToolsVersion rootProject.ext.buildToolsVersion
    } else {
        logger.warn('compileSdkVersion not found in rootProject')
        compileSdkVersion 23
        buildToolsVersion "23.0.0 rc3"
    }

    defaultConfig {
        applicationId "de.j4velin.wifiAutoOff"
    }

    signingConfigs {
        release
    }

    productFlavors {
        play
        fdroid
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        debug {
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }

    lintOptions {
        disable "ExtraTranslation"
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.4.0'
    playCompile 'com.android.support:cardview-v7:23.4.0'
    playCompile 'com.android.support:recyclerview-v7:23.4.0'
    playCompile 'com.shamanland:fab:0.0.7'
    playCompile 'com.google.android.gms:play-services-location:8.4.0'
    playCompile 'com.google.android.gms:play-services-maps:8.4.0'
}

def props = new Properties()

props.load(new FileInputStream(project.file("key.properties")))

android.signingConfigs.release.storeFile project.file(props.keyStore)

android.signingConfigs.release.storePassword props.keyStorePassword

android.signingConfigs.release.keyAlias props.keyAlias

android.signingConfigs.release.keyPassword props.keyAliasPassword