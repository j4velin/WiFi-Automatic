buildscript {
    repositories {
        google()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        jcenter()
    }
    dependencies {
        if (rootProject.ext.has("gradle_classpath")) {
            classpath rootProject.ext.gradle_classpath
        } else {
            logger.warn('classpath not found in rootProject')
            classpath 'com.android.tools.build:gradle:3.0.1'
        }
    }
}

repositories {
    google()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
    jcenter()
}

apply plugin: 'com.android.application'

android {

    if (rootProject.ext.has("compileSdkVersion")) {
        compileSdkVersion rootProject.ext.compileSdkVersion
        buildToolsVersion rootProject.ext.buildToolsVersion
    } else {
        logger.warn('compileSdkVersion not found in rootProject')
        compileSdkVersion 27
        buildToolsVersion "27.0.3"
    }

    defaultConfig {
        applicationId "de.j4velin.wifiAutoOff"
        if (!project.file("key.properties").exists()) {
            resValue "string", "maps_api_key", "0000"
        }
    }

    signingConfigs {
        release
    }

    flavorDimensions "main"

    productFlavors {
        play {
            dimension "main"
        }
        fdroid {
            dimension "main"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        debug {
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }

    lintOptions {
        disable "ExtraTranslation"
        if (System.getenv("TRAVIS")) {
            ignoreWarnings true
            abortOnError false
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    playImplementation 'com.android.support:cardview-v7:27.1.1'
    playImplementation 'com.shamanland:fab:0.0.8'
    playImplementation 'com.google.android.gms:play-services-location:15.0.1'
    playImplementation 'com.google.android.gms:play-services-maps:15.0.1'
}

def props = new Properties()

if (project.file("key.properties").exists()) {
    props.load(new FileInputStream(project.file("key.properties")))
} else {
    logger.warn('Using sample keystore!!')
    props.load(new FileInputStream(project.file("key.properties.sample")))
}

android.signingConfigs.release.storeFile project.file(props.keyStore)

android.signingConfigs.release.storePassword props.keyStorePassword

android.signingConfigs.release.keyAlias props.keyAlias

android.signingConfigs.release.keyPassword props.keyAliasPassword